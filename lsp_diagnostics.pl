/*  Author:        Jan Wielemaker
    E-mail:        jan@swi-prolog.org
    WWW:           http://www.swi-prolog.org
    Copyright (c)  2025, SWI-Prolog Solutions b.v.
    All rights reserved.

    Redistribution and use in source and binary forms, with or without
    modification, are permitted provided that the following conditions
    are met:

    1. Redistributions of source code must retain the above copyright
       notice, this list of conditions and the following disclaimer.

    2. Redistributions in binary form must reproduce the above copyright
       notice, this list of conditions and the following disclaimer in
       the documentation and/or other materials provided with the
       distribution.

    THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
    "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
    LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
    FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
    COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
    INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
    BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
    LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
    CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
    LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
    ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
    POSSIBILITY OF SUCH DAMAGE.
*/

:- module(lsp_diagnostics,
          [ lsp_diagnostic_style/2,  % ?StyleName, ?StyleProperties
            lsp_severity_type/3      % ?LSPLevel, ?LSPName, ?FragmentStyle
          ]).
:- use_module(library(pce)).
:- use_module(library(doc/objects)). % @br, etc.
:- use_module(library(hyper)).
:- use_module(library(apply)).
:- use_module(library(debug)).
:- use_module(library(lists)).
:- use_module(library(pce_util)).
:- use_module(library(uri)).

:- use_module(lsp_client).

/** <module> Handle LSP disagnostic messages

This  module  provides  the  infrastructucture  to  deal  with  the  LSP
initiated ``textDocument/publishDiagnostics()`` method. It extends class
`emacs_buffer` to create `emacs_lsp_diagnostic`  fragments, hovering and
selecting fragments and initiating "fix available" edits.
*/

%!  lsp_severity_type(?LSPLevel, ?LSPName, ?FragmentStyle).

lsp_severity_type(1, error,   lsp_diag_error).
lsp_severity_type(2, warning, lsp_diag_warning).
lsp_severity_type(3, info,    lsp_diag_info).
lsp_severity_type(4, hint,    lsp_diag_hint).

:- det(style_pce_severity/3).
style_pce_severity(lsp_diag_error,   error,   'Error').
style_pce_severity(lsp_diag_warning, warning, 'Warning').
style_pce_severity(lsp_diag_info,    status,  'Information').
style_pce_severity(lsp_diag_hint,    status,  'Hint').

%!  lsp_diagnostic_style(?StyleName, ?StyleProperties) is nondet.
%
%   Provide style name and properties for the LSP diagnostic fragments.

lsp_diagnostic_style(lsp_diag_error,   [icon(Icon), underline(red)]) :-
    lsp_icon(error, Icon).
lsp_diagnostic_style(lsp_diag_warning, [icon(Icon), underline(orange)]) :-
    lsp_icon(warning, Icon).
lsp_diagnostic_style(lsp_diag_info,    [icon(Icon), underline(yellow)]) :-
    lsp_icon(info, Icon).
lsp_diagnostic_style(lsp_diag_hint,    [icon(Icon), underline(navyblue)]) :-
    lsp_icon(hint, Icon).

lsp_icon(error,   '64x64/lsp-error.png').
lsp_icon(warning, '64x64/lsp-warning.png').
lsp_icon(info,    '64x64/lsp-information.png').
lsp_icon(hint,    '64x64/lsp-hint.png').


                /*******************************
                *     EXTEND LANGUAGE MODE     *
                *******************************/

:- emacs_extend_mode(language,
                     [ goto_next_error = key('\\egn'),
                       goto_prev_error = key('\\egp')
                     ]).

selected_fragment(M, Fragment:fragment) :->
    "User selected a fragment in the margin"::
    send(M, show_fragment_note, Fragment).

hover_fragment_icon(M, Fragment:fragment*, _Area:[area]) :->
    "User selected a fragment in the margin"::
    (   Fragment == @nil
    ->  ignore(send(M, send_hyper, note, hover_end))
    ;   send(M, send_hyper, note, explicitly_opened)
    ->  true
    ;   send(M, show_fragment_note, Fragment, @on)
    ).

show_fragment_note(M, Fragment:fragment, Hover:[bool]) :->
    "Show message associated with a fragment below the fragment"::
    (   send(Fragment, instance_of, emacs_lsp_diagnostic)
    ->  get(M, editor, E),
        ignore(send(M, send_hyper, note, destroy)),
        new(W, emacs_lsp_diagnostic_window(E, Fragment, Hover)),
        new(_, partof_hyper(M, W, note, mode)),
        send(W, open)
    ;   true
    ).

goto_lsp_diagnostic(M, Dir:direction={next,prev}) :->
    "Goto the next/prev LSP diagnostic"::
    get(M, caret, Caret),
    (   (   Dir == next
        ->  get(M, find_fragment,
                and(message(@arg1, instance_of, emacs_lsp_diagnostic),
                    @arg1?start > Caret),
                Next)
        ;   get(M, find_all_fragments,
                and(message(@arg1, instance_of, emacs_lsp_diagnostic),
                    @arg1?end   < Caret),
                All),
            get(All, tail, Next)
        )
    ->  get(Next, start, Start),
        get(Next, end, End),
        send(M, selection, End, Start, highlight)
    ;   send(M, report, status, "No further diagnostic messages")
    ).

goto_next_error(M) :->
    "Go to the next LSP diagnostic"::
    send(M, goto_lsp_diagnostic, next).

goto_prev_error(M) :->
    "Go to the previous LSP diagnostic"::
    send(M, goto_lsp_diagnostic, prev).

:- emacs_end_mode.


                /*******************************
                *     EXTEND EMACS BUFFER      *
                *******************************/

:- dynamic
    session_dictionary/1.

:- pce_extend_class(emacs_buffer).

lsp_publish_diagnostics(Buffer, LSP:lsp=lsp_client,
                        Diagnostics:diagnostics=prolog,
                        Region:lsp_region_fragment*) :->
    "Create fragments from diagnostics"::
    send(Buffer, lsp_clear_diagnostics, Region),
    (   Region == @nil
    ->  LineOffset = 0
    ;   get(Region, start, Start),
        get(Buffer, line_number, Start, Line1),
        LineOffset is Line1-1
    ),
    State = counts(0,0,0,0),
    maplist(show_diagnostic(Buffer, LSP, State, LineOffset),
            Diagnostics),
    report_diagnostic_counts(State, Buffer),
    debug(lsp(diagnostics), 'Counts: ~p', [State]).

%!  report_diagnostic_counts(+Counts, +Buffer) is det.
%
%   Report diagnostic message count if there are diagnostics.
%
%   @tbd: If we just did a region, should we report for the entire file?

report_diagnostic_counts(Counts, Buffer) :-
    (   Counts == counts(0,0,0,0)
    ->  true
    ;   send(Buffer?editors, for_all,
             message(@arg1, lsp_enable_margin, @on)),
        Counts = counts(E,W,I,H),
        (   E == 0
        ->  Level = status
        ;   Level = warning
        ),
        send(Buffer, report, Level,
             'E:%d, W:%d, I:%d, H:%d', E,W,I,H)
    ).

show_diagnostic(Buffer, LSP, State, LineOffset, Diagnostic) :-
    #{range: Range, severity: Severity} :< Diagnostic,
    #{start: Start, end: End} :< Range,
    lsp_offset(Start, Buffer, LineOffset, StartOffset),
    lsp_offset(End, Buffer, LineOffset, EndOffset),
    Length is EndOffset-StartOffset,
    (   suppressed(Buffer, LSP, StartOffset, Length, Diagnostic)
    ->  true
    ;   lsp_severity_type(Severity, _Name, Style),
        step_count(Severity, State),
        new(D, emacs_lsp_diagnostic(Buffer, StartOffset, Length,
                                    Diagnostic, Style)),
        send(D, slot, lsp_client, LSP)
    ).

lsp_offset(#{line:Line, character:Char}, Buffer, LineOffset, Offset) =>
    TheLine is Line+LineOffset,
    get(Buffer, lsp_offset, TheLine, Char, Offset).

step_count(Severity, State) :-
    arg(Severity, State, C0),
    C is C0+1,
    nb_setarg(Severity, State, C).

lsp_clear_diagnostics(Buffer, Region:lsp_region_fragment*) :->
    "Remove emacs_lsp_diagnostic fragments [in region]"::
    (   Region == @nil
    ->  send(Buffer, for_all_fragments,
             if(message(@arg1, instance_of, emacs_lsp_diagnostic),
                message(@arg1, free)))
    ;   send(Buffer, for_all_fragments,
             if(and(message(@arg1, instance_of, emacs_lsp_diagnostic),
                    message(@arg1, overlap, Region)),
                message(@arg1, free)))
    ).


suppressed(Buffer, LSP, StartOffset, Length, Diagnostic) :-
    get(LSP, config, Config),
    SpellingCodes = Config.get(spelling).get(codes),
    atom_string(SpellingCode, Diagnostic.get(code)),
    memberchk(SpellingCode, SpellingCodes),
    get(Buffer, contents, StartOffset, Length, string(String)),
    atom_string(Word, String),
    session_dictionary(Word).

:- pce_end_class.


                /*******************************
                *           FRAGMENT           *
                *******************************/

:- pce_begin_class(emacs_lsp_diagnostic, fragment,
                   "Represent an LSP diagnostic message").

variable(lsp_client, lsp_client*, get, "Source LSP client").
variable(json,       prolog,      get, "JSON diagnostic message").

initialise(F, Buffer:text_buffer, Start:int, Len:int,
           JSON:prolog, Style:name) :->
    "Create an LSP diagnostic fragment"::
    send_super(F, initialise, Buffer, Start, Len, Style),
    send(F, slot, json, JSON).

message(F, Msg:string) :<-
    "Diagnostic message"::
    get(F, json, Dict),
    Msg = Dict.get(message, "No message").

identify(F) :->
    "Show LSP message"::
    get(F, style, Style),
    style_pce_severity(Style, Severity, Label),
    send(F?text_buffer, report, Severity, '%s: %s', Label, F?message).

source(F, Source:name) :<-
    "Get the origin of the diagnostic"::
    get(F?lsp_client, id, Source).

code(F, Code:name) :<-
    "Get the diagnostic code"::
    get(F, json, Dict),
    atom_string(Code, Dict.get(code)).

range(F, Range:prolog) :<-
    "Get the original range as Prolog dict"::
    get(F, json, Dict),
    Range = Dict.get(range).

lsp_class(F, LspClass:{error,warning,info,hint}) :<-
    "Get severity class"::
    get(F, style, Style),
    style_pce_severity(Style, LspClass, _Label).

icon(F, Icon:image) :<-
    get(F, style, Style),
    lsp_severity_type(_Level, LspClass, Style),
    lsp_icon(LspClass, Icon).

fixes(F, Fixes:prolog) :<-
    "Get fixes from LSP server"::
    get(F, text_buffer, Buffer),
    get(Buffer, attribute, lsp_tracking, URI),
    get(F, json, Diagnostic),
    debug(lsp(code_action), 'Requesting code actions for ~@',
          [print_term(Diagnostic, [output(current_output)])]),
    get(F, lsp_client, LSP),
    Context0 = #{ diagnostics: [Diagnostic] },
    (   get(LSP, code_action_kinds, Kinds)
    ->  Context = Context0.put(only, Kinds)
    ;   Context = Context0
    ),
    get(LSP, call,
        'textDocument/codeAction'(
            #{ textDocument: #{ uri: URI},
               range: Diagnostic.range,
               context: Context
             }),
        Fixes),
    debug(lsp(code_action), 'Code actions: ~@',
          [print_term(Fixes, [output(current_output)])]).

:- pce_end_class.


:- pce_begin_class(lsp_region_fragment, fragment,
                   "Used to mark region for an LSP to work on").

variable(uri,	     name,       get, "Region URI").
variable(lsp_client, lsp_client, get, "LSP we are connected to").
variable(file,	     file,       get, "(Tmp) file connected").

initialise(Region, TB:emacs_buffer, LSP:lsp_client) :->
    send_super(Region, initialise, TB, 0, 0, lsp_region),
    get(TB, attribute, lsp_tracking, DocumentURI),
    file_name_extension(_, Ext, DocumentURI),
    tmp_file_stream(TmpFile, Stream,
                    [ encoding(utf8),
                      extension(Ext)
                    ]),
    close(Stream),
    uri_file_name(RegionURI, TmpFile),
    send(Region, slot, uri,        RegionURI),
    send(Region, slot, lsp_client, LSP),
    send(Region, slot, file,       TmpFile),
    get(TB, mode, Mode),
    send(LSP, notify,
         'textDocument/didOpen'(
             #{textDocument:
                 #{ uri: RegionURI,
                    languageId: Mode,
                    version: 1,
                    text: ""
                  }
              })),
    debug(vale(region), 'Established region using ~q', [TmpFile]).

range(Region, Start:start=int, End:end=int) :->
    "Set extends"::
    send(Region, start, Start, @off),
    send(Region, end, End).

did_save(Region) :->
    "Save content to file and notify LSP using didSave()"::
    get(Region, text_buffer, TB),
    get(TB, attribute, lsp_tracking, DocumentURI),
    get(Region, string, string(Text)),
    get(Region, start, Start),
    get(Region, end, End),
    send(TB, do_save, Region?file, Start, End),
    get(Region, uri, RegionURI),
    debug(vale(region), 'Using didSave to check ~d..~d',
          [Start, End]),
    lsp_set_document_region(RegionURI, DocumentURI, Region),
    send(Region?lsp_client, notify,
         'textDocument/didSave'(
             #{ textDocument:
                  #{ uri: RegionURI
                   },
                text: Text
              })).

:- pce_end_class.


                /*******************************
                *            WINDOW            *
                *******************************/

:- pce_begin_class(emacs_lsp_diagnostic_window, dialog,
                   "Show LSP diagnostics in modal window").

variable(lsp_client, lsp_client*, get, "Source LSP client").

class_variable(text_width, int, 400,
               "Width for layout of diagnostic message").

initialise(W, Editor:editor, Fragment:emacs_lsp_diagnostic,
           Hover:[bool]) :->
    get(Fragment, start, Offset),
    get(Editor, image, TextImage),
    get(TextImage, character_position, Offset, point(X,Y)),
    get(TextImage, frame_position, point(OX,OY)),
    get(Editor, frame, Master),
    send_super(W, initialise, "LSP Feedback"),
    send(W, slot, lsp_client, Fragment?lsp_client),
    send(W, transient_for, Master),
    send(W, kind, popup),
    get(Fragment, icon, Icon),
    send(W, append, new(I, label(icon, image(Icon)))),
    get(W, class_variable_value, text_width, TW),
    send(W, append, new(G, dialog_group(message,group)), right),
    send(G, append, new(M, parbox(TW, left))),
    send(M, name, message),
    send_list([I,G], reference, point(0,0)),
    (   Hover == @on
    ->  true
    ;   send(W, fixes_buttons, Fragment),
        send(W, append, new(Done, button(done, message(W, destroy)))),
        send(W, keyboard_focus, Done)
    ),
    send(W, message, Fragment?message),
    new(_, partof_hyper(Fragment, W, dialog, fragment)),
    send(W?frame, position, point(OX+X, OY+Y+2)).

destroy(W) :->
    "Allow calling from a thread"::
    (   thread_self(Me),
        pce_thread(Me)
    ->  send_super(W, destroy)
    ;   in_pce_thread(send(W, destroy))
    ).

explicitly_opened(W) :->
    "User opened this using a click"::
    get(W, member, done, _Button).

hover_end(W) :->
    "Mouse left the icon"::
    (   send(W, explicitly_opened)
    ->  true
    ;   send(W, destroy)
    ).

message(W, Msg:string) :->
    "Update the message"::
    get(W, member, message, Group),
    get(Group, member, message, PB),
    object(Msg, string(Message)),
    split_string(Message, '\n', '', Lines),
    append_pars(Lines, PB).

append_pars([], _) =>
    true.
append_pars([Last], PB) =>
    send(PB, cdata, Last).
append_pars([H|T], PB) =>
    send(PB, cdata, H),
    send_list(PB, append, [@nbsp,@br]),
    append_pars(T, PB).

%   ->fixes_buttons(Fragment)
%
%   Add buttons for the proposed fixes.

fixes_buttons(W, Fragment:emacs_lsp_diagnostic) :->
    "Add buttons for available fixes"::
    get(Fragment, fixes, Fixes),
    (   Fixes == [], fail
    ->  true
    ;   get(Fragment, range, Range),
        same_diagnostics(Fragment, All),
        get(Fragment, string, string(Text)),
        length(All, Count),
        get(W, member, message, MsgGroup),
        send(MsgGroup, append,
             new(Group, dialog_group(buttons, group)),
             next_row),
        (   member(Fix, Fixes),
            append_fix_button(W, Group, Range, Text, Count, Fix),
            fail
        ;   true
        ),
        send(W, add_dictionary_buttons, Fragment)
    ).

action_button(W, Icon:image, Title:char_array, Msg:code) :->
    "Add button with icon"::
    get(W, member, message, MsgGroup),
    get(MsgGroup, member, buttons, Group),
    send(Group, append,
         new(LBL, label(icon, Icon)),
         next_row),
    send(Group, append,
         new(B, button(Title, Msg)),
         right),
    send(LBL, width, 32),
    send(LBL, reference, point(0, B?reference?y)),
    send(B, alignment, left).

append_fix_button(W, Group, Range, Text, Count, Fix),
    fix_command(Fix, Range, Title, Command, Args, Kind) =>
    debug(lsp(fix), "Fix: ~@",
          [print_term(Fix, [output(current_output)])]),
    fix_icon(Command, Kind, Icon),
    send(W, action_button,
         image(Icon), Title,
         message(W, apply_change, Title)),
    add_replace_all(Count, Command, Args, Text, W, Group).
append_fix_button(_W, _Group, _Range, _Text, _Count, Fix) =>
    debug(lsp(unknown_fix), "Unknown fix: ~@",
          [print_term(Fix, [output(current_output)])]).

%!  add_replace_all(+Count, +Command, +Args, +OrgText, +Windog, +Group)
%
%   Add a replace all button if
%
%     - There are more than one equivalent changes
%     - The command just edits the range
%     - It is not a case-change.

add_replace_all(1, _, _, _, _, _) :-
    !.
add_replace_all(N, "pce_emacs.edit", replace(With), Text, W, Group) :-
    \+ ( string_lower(With, Lower),
         string_lower(Text, Lower)
       ),
    !,
    format(string(Label), 'Replace all ~D occurrences', [N]),
    send(Group, append,
         new(B, button(Label, message(W, replace_all, With))),
         right),
    send(B, alignment, column).
add_replace_all(_, _, _, _, _, _).

%!  fix_command(+CodeAction, +Range, -Title, -Command, -Args, -Kind) is
%!              semidet.
%
%   If  the  command  is  an  immediate  edit,  Command  is  unified  to
%   `"pce_emacs.edit"`.

fix_command(Fix, _Range, Title, Command, Args, Kind),
    #{ command: CommandDict, title: Title } :< Fix,
    is_dict(CommandDict),
    #{ command: Command, arguments: Args } :< CommandDict =>
    Kind = Fix.get(kind, "unknown").
fix_command(Fix, Range, Title, Command, Edits, Kind),
    #{ edit: Edit, title: Title } :< Fix,
    fix_edits(Edit, Range, Edits) =>
    Command = "pce_emacs.edit",
    Kind = Fix.get(kind, "unknown").
fix_command(Fix, _Range, Title, Command, Args, Kind),
    #{ command: Command, arguments: Args, title: Title } :< Fix =>
    Kind = Fix.get(kind, "unknown").
fix_command(_Fix, _Range, _Title, _Command, _Args, _Kind) =>
    fail.

%!  fix_edits(+Dict, +Range, -Edits:dict) is semidet.
%
%   Normalize the `edit` field  to  a   dict  holding  `changes`, a dict
%   mapping document URIs to a list  of   changes.  If  the edit exactly
%   replaces  the  diagnostics  fragment,   Edits    is   unified   with
%   replace(NewText). This detection allows for batch replacement of all
%   equivalent diagnostics.

fix_edits(Dict, Range, Edits) :-
    fix_edits(Dict, Edits0),
    (   dict_pairs(Edits0.get(changes), _, [_URI-[Change]]),
        #{newText:Replace, range:Range} :< Change
    ->  Edits = replace(Replace)
    ;   Edits = Edits0
    ).

fix_edits(Dict, Edits),
    is_dict(Dict),
    #{ changes: _ } :< Dict =>
    Edits = Dict.
fix_edits(Dict, Edits),
    is_dict(Dict),
    #{ documentChanges: Edits0 } :< Dict =>
    Edits = Edits0.
fix_edits(_, _) =>
    fail.


%!  fix_icon(+Command:string, +Kind:string, -Icon) is det.

fix_icon("clangd.applyTweak", _, '64x64/lsp-apply-tweak.png') :- !.
fix_icon("clangd.applyFix",   _, '64x64/lsp-apply-fix.png')   :- !.
fix_icon(_,                   _, '64x64/lsp-apply-fix.png').

%   ->apply_change(+Title)
%
%   Apply the selected code action.
%
%   (*) Note that killing the fragment also kills this window.

apply_change(W, TitleObj:string) :->
    "Apply a selected change"::
    get(W, get_hyper, fragment, fixes, Fixes),
    get(W, get_hyper, fragment, range, Range),
    object(TitleObj, string(TitleAtom)),
    atom_string(TitleAtom, Title),
    (   member(Fix, Fixes),
        fix_command(Fix, Range, Title, Command, Args, _Kind)
    ->  true
    ),
    get(W, lsp_client, LSP),
    (   Command = "pce_emacs.edit",
        Args = replace(NewText)
    ->  debug(lsp(replace), 'Direct replace with ~p', [NewText]),
        send(W, send_hyper, fragment, string, NewText),
        send(W, send_hyper, fragment, free)
    ;   send(W, send_hyper, fragment, free),	% see (*)
        send(LSP, execute_command, Command, Args)
    ).

replace_all(W, With:char_array) :->
    "Replace all \"same\" diagnostics"::
    get(W, hypered, fragment, Fragment),
    same_diagnostics(Fragment, All),
    forall(member(F, All),
           ( send(F, string, With),
             send(F, free)
           )).

:- pce_group(spelling).

% Deal   with   suppressing   and   dictionary   additions.   Some   LSP
% implementation do this for you,  some  don't.   In  that  case the LSP
% registration should contain a key `spelling` with values `code` set to
% the diagnostic code emitted  for   spelling  errors  and `dictionary`,
% pointing at a file that is used for the user dictionary.

%   ->add_dictionary_buttons(+Fragment)
%
%   Added _accept_ and _dictionary_  buttons  if   this  is  a  spelling
%   correction action and the LSP registry asks us to.

add_dictionary_buttons(W, Fragment:emacs_lsp_diagnostic) :->
    "Add buttons for accept and dictionary"::
    get(Fragment, code, Code),
    get(Fragment, lsp_client, LSP),
    get(LSP, config, Dict),
    memberchk(Code, Dict.get(spelling).get(codes)),
    get(Fragment?string, value, Word),
    send(W, action_button, image('64x64/dictionary.png'),
         'Accept (session)', message(W, accept, Word, session)),
    send(W, action_button, image('64x64/dictionary.png'),
         add_to_dictionary, message(W, accept, Word, dictionary)).

accept(W, Word:name, Scope:{session,dictionary}) :->
    "Accept a word marked as spelling error"::
    get(W, hypered, fragment, Fragment),
    (   Scope == session
    ->  asserta(session_dictionary(Word))
    ;   get(Fragment, lsp_client, LSP),
        get(LSP, config, Dict),
        FileSpec = Dict.get(spelling).get(dictionary),
        dictionary_file(FileSpec, File),
        setup_call_cleanup(
            open(File, append, Out,
                 [ encoding(utf8)
                 ]),
            format(Out, '~w~n', [Word]),
            close(Out))
    ),
    same_diagnostics(Fragment, All),
    forall(member(F, All),
           send(F, free)).

dictionary_file(FileSpec, File) :-
    atomic(FileSpec),
    expand_file_name(FileSpec, [File]).
dictionary_file(FileSpec, File) :-
    absolute_file_name(FileSpec, File,
                       [ access(write)
                       ]).

:- pce_end_class.

%!  same_diagnostics(+To:emacs_lsp_diagnostic, -List) is det.
%
%   Find equivalent diagnostic messages.

:- det(same_diagnostics/2).
same_diagnostics(To, List) :-
    get(To, string, Text),
    get(To, source, Source),
    get(To, code, Code),
    get(To, text_buffer, TB),
    get(TB, first_fragment, Start),
    same_diagnostics(Start, Text, Source, Code, List).

same_diagnostics(Frag, Text, Source, Code, List) :-
    (   send(Frag, instance_of, emacs_lsp_diagnostic),
        send(Frag?string, equal, Text),
        get(Frag, source, Source),
        get(Frag, code, Code)
    ->  List = [Frag|Tail]
    ;   Tail = List
    ),
    (   get(Frag, next, message(@arg1, instance_of, emacs_lsp_diagnostic),
            Next)
    ->  same_diagnostics(Next, Text, Source, Code, Tail)
    ;   Tail = []
    ).
